

pathDir("v"). 
pathDir("o").

usedEdge(X,Y,X1,Y1) | notUsedEdge(X,Y,X1,Y1) :- giveStart(X,Y), adjAllDir(X,Y,X1,Y1,D), pathDir(D), empty(X1,Y1).
usedEdge(X,Y,X1,Y1) | notUsedEdge(X,Y,X1,Y1) :- usedEdge(_,_,X,Y), adjAllDir(X,Y,X1,Y1,D), pathDir(D), empty(X1,Y1).

:~ usedEdge(_,_,_,_). [1@2]
:~ used(X,Y). [1@1]

reachable(X,Y,X1,Y1):-usedEdge(X,Y,X1,Y1).
reachable(X,Y,X2,Y2):- reachable(X,Y,X1,Y1), usedEdge(X1,Y1,X2,Y2).
:- giveStart(X,Y), giveEnd(X1,Y1), not reachable(X,Y,X1,Y1).

giveUsed(X,Y) :- used(X,Y).

used(X,Y) :- usedEdge(X,Y,_,_).
used(X,Y) :- usedEdge(_,_,X,Y).

:- used(X,Y), #count{X1,Y1: used(X1,Y1), adjAllDir(X,Y,X1,Y1,D), pathDir(D)}=A, 
	#count{X1,Y1: giveStart(X1,Y1), adjAllDir(X1,Y1,X,Y,D), pathDir(D)}=B, A+B>2, not giveStart(X,Y), not giveEnd(X,Y).
:- used(X,Y), #count{X1,Y1: used(X1,Y1), adjAllDir(X,Y,X1,Y1,D), pathDir(D)}=0, not giveStart(X,Y), not giveEnd(X,Y).
:- giveStart(X,Y), #count{X1,Y1: used(X1,Y1), adjAllDir(X,Y,X1,Y1,D), pathDir(D)}<>1.

% non è possibile che 2 archi usati partano dalla stessa cella 
:- usedEdge(X,Y,X1,Y1), usedEdge(X,Y,X2,Y2), not celleUguali(X1,Y1,X2,Y2). 

% non è possibile che 2 archi usati arrivino nella stessa cella 
:- usedEdge(X1,Y1,X,Y), usedEdge(X2,Y2,X,Y), not celleUguali(X1,Y1,X2,Y2). 

% da giveEnd non deve uscire niente
:- giveEnd(X,Y), usedEdge(X,Y,_,_).

% non si devono utilizzare due archi vicini
:- usedEdge(X,Y,X1,Y1), usedEdge(X1,Y1,X2,Y2), usedEdge(X2,Y2,X3,Y3), adjAllDir(X,Y,X3,Y3,D), pathDir(D).
